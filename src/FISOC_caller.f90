
PROGRAM FISOC_main

  USE ESMF
  USE FISOC_utils
  USE FISOC_parent_mod, ONLY : FISOC_parent_register

  IMPLICIT NONE

  
!------------------------------------------------------------------------------

  ! return code (generated by ESMF) and user return code (generated by user 
  ! code and passed through ESMF) 
  INTEGER :: rc, urc
  INTEGER :: fileunit

  ! Timekeeping
  TYPE(ESMF_Clock)        :: FISOC_clock
  INTEGER                 :: ISM_dt_sec, OM_dt_sec, dt_ratio
  INTEGER                 :: start_year, end_year, start_month, end_month
  INTEGER                 :: OM_outputInterval
  TYPE(ESMF_TimeInterval) :: ISM_dt, OM_dt
  TYPE(ESMF_Time)         :: startTime, endTime
  TYPE(ESMF_Alarm)        :: alarm_OM, alarm_OM_output, alarm_ISM, alarm_ISM_exportAvailable
  LOGICAL                 :: tight_coupling

  ! A parent gridded component is used to support the hierarchical approach  
  ! of ESMF, but the actual grid and state are dummy properties.  The parent 
  ! merely coordinates the child components (ice, ocean and processing)
  TYPE(ESMF_GridComp)     :: FISOC_parent 
  TYPE(ESMF_config)       :: FISOC_config
  TYPE(ESMF_state)        :: importstate, exportstate

!------------------------------------------------------------------------------


! ***some things ESMF offers:
! logging (you don't need to create a log, ESMF will create a default log on initialize, 
! but you can create further logs using ESMF if you want) 
! regridding
! clocks and alarms
! helping to manage effective parallelization

!------------------------------------------------------------------------------
!*** what calendar? 360 day?
  ! initialize ESMF framework
  CALL ESMF_Initialize(defaultCalKind=ESMF_CALKIND_GREGORIAN, &
       defaultlogfilename="FISOC.Log", &
       logkindflag=ESMF_LOGKIND_MULTI, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) THEN
     CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  END IF
  msg = "Initialised ESMF framework"  
  CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
       line=__LINE__, file=__FILE__, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  ! Note: we are only checking log writing for success on first call to ESMF_LogWrite
  ! possibly this is naively optimistic...


  ! Load configuration file
  FISOC_config = ESMF_ConfigCreate(rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  call ESMF_ConfigLoadFile(FISOC_config, "FISOC_config.rc", rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  msg = "Loaded FISOC configuration file"  
  CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
       line=__LINE__, file=__FILE__, rc=rc)
  

  !------------------------------------------------------------------------------

  ! Create the parent Gridded Component (though we don't use its grid, we just 
  ! use it for coordinating child components...)
  FISOC_parent = ESMF_GridCompCreate(name="FISOC parent gridded component", config=FISOC_config, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  ! Register parent (parent registers run, init and fin routines, and child comps) 
  CALL ESMF_GridCompSetServices(FISOC_parent, FISOC_parent_register, &
       userRc=urc, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  
  msg = "Completed FISOC_parent creation and registration"  
  CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
       line=__LINE__, file=__FILE__, rc=rc)
  

  !------------------------------------------------------------------------------
  ! FISOC time model 
  ! 
  ! FISOC runs one clock which increments with the ocean timestep. The ice 
  ! timestep must be a multiple of the ocean timestep.  An ocean alarm is used 
  ! to indicate whether the ocean model should be run.  The alarm is event driven 
  ! and is set to always on for tightly coupled simulations.  An ice alarm is 
  ! used to indicate whether the ice model should be run.  This is a periodic 
  ! alarm.
  !
  ! ***we could also have a clock in each component and do some checks that the 
  ! clocks are on the same time?
  !
  ! ***loose coupling needs more thought - logical switch sent to FISOC_proc state?
  !
  ! FISOC time related variables:
  ! FISOC_clock          the main FISOC clock
  ! alarm_ISM            the (periodic) alarm for the ice model 
  ! alarm_OM            the (event driven) alarm for the ocean model
  ! tight_coupling (config) whether tight coupling (always call ocean) or loose 
  !                      coupling (call ocean after large geometry change) 
  ! OM_dt_sec (config)   ocean timestep in seconds
  ! dt_ratio   (config)  timestep ratio 
  ! ISM_dt_sec           ice timestep in seconds  (=OM_dt_sec*dt_ratio)
  ! OM_dt                ocean timestep in ESMF format
  ! ISM_dt               ice timestep in ESMF format
  ! start_year  (config) start and end dates for FISOC simulation
  ! start_month (config) ...
  ! end_year    (config) ...
  ! end_month   (config) ...
  !
  ! variables marked (config) are set in the FISOC_config file.
  !
  CALL ESMF_ConfigGetAttribute(FISOC_config, dt_ratio, label='dt_ratio:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ConfigGetAttribute(FISOC_config, OM_dt_sec, label='OM_dt_sec:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL FISOC_ConfigDerivedAttribute(FISOC_config, ISM_dt_sec, 'ISM_dt_sec',rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ConfigGetAttribute(FISOC_config, start_month, label='start_month:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ConfigGetAttribute(FISOC_config, start_year, label='start_year:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ConfigGetAttribute(FISOC_config, end_month, label='end_month:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ConfigGetAttribute(FISOC_config, end_year, label='end_year:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__, rcToReturn=rc)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ConfigGetAttribute(FISOC_config, OM_outputInterval, label='OM_outputInterval:', rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  
  IF (MOD(dt_ratio,OM_outputInterval).NE.0) THEN
     msg = "ERROR: dt_ratio/OM_outputInterval is required to be integer"
     CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
          line=__LINE__, file=__FILE__, rc=rc)
     CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  END IF
  
  !
  !------------------------------------------------------------------------------
  ! setting up clocks and alarms
  !
  CALL ESMF_TimeIntervalSet(OM_dt, s=OM_dt_sec, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  CALL ESMF_TimeIntervalSet(ISM_dt, s=ISM_dt_sec, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  CALL ESMF_TimeSet(startTime, yy=start_year, mm=start_month, dd=1, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  CALL ESMF_TimeSet(endTime, yy=end_year, mm=end_month, dd=1, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  FISOC_clock = ESMF_ClockCreate(OM_dt, startTime, stopTime=endTime, &
       name="FISOC main clock", rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  alarm_OM = ESMF_AlarmCreate(clock=FISOC_clock, name="alarm_OM", &
       ringTime=startTime, ringInterval=OM_dt*OM_outputInterval, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  alarm_OM_output = ESMF_AlarmCreate(clock=FISOC_clock, name="alarm_OM_output", &
       ringTime=startTime, ringInterval=OM_dt, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)

  alarm_ISM = ESMF_AlarmCreate(clock=FISOC_clock, name="alarm_ISM", &
       ringTime=((startTime+ISM_dt)-OM_dt), ringInterval=ISM_dt, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)
  
  alarm_ISM_exportAvailable = ESMF_AlarmCreate(clock=FISOC_clock, &
       name="alarm_ISM_exportAvailable", &
       ringTime=(startTime+ISM_dt), ringInterval=ISM_dt, rc=rc)
  IF (rc /= ESMF_SUCCESS) CALL ESMF_Finalize(endflag=ESMF_END_ABORT, rc=rc)
  
  msg = "created and initialised clocks and alarms"  
  CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
       line=__LINE__, file=__FILE__, rc=rc)


  !------------------------------------------------------------------------------
  ! The main bit: initialize, run and finalize FISOC_parent (the parent calls 
  ! all child components).
  importState = ESMF_StateCreate(name='parent import state', stateintent=ESMF_STATEINTENT_IMPORT, rc=rc) 
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  exportState = ESMF_StateCreate(name='parent export state', stateintent=ESMF_STATEINTENT_IMPORT, rc=rc) 
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_GridCompInitialize(FISOC_parent, &
       importState=importState, exportState=exportState, &
       clock=FISOC_clock, userRc=urc, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  
  CALL ESMF_GridCompRun(FISOC_parent, &
       importState=importState, exportState=exportState, &
       clock=FISOC_clock, userRc=urc, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  !------------------------------------------------------------------------------
  msg = "FISOC run complete, tidying up..."  
  CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
       line=__LINE__, file=__FILE__, rc=rc)

  CALL ESMF_GridCompFinalize(FISOC_parent, &
       importState=importState, exportState=exportState, &
       clock=FISOC_clock, userRc=urc, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  
  !------------------------------------------------------------------------------
  CALL ESMF_AlarmDestroy(alarm_ISM, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  
  CALL ESMF_AlarmDestroy(alarm_OM, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)

  CALL ESMF_ClockDestroy(FISOC_clock, rc=rc)
  IF (ESMF_LogFoundError(rcToCheck=rc, msg=ESMF_LOGERR_PASSTHRU, &
       line=__LINE__, file=__FILE__)) &
       CALL ESMF_Finalize(endflag=ESMF_END_ABORT)
  
  msg = "FISOC finished"  
  CALL ESMF_LogWrite(msg, logmsgFlag=ESMF_LOGMSG_INFO, &
       line=__LINE__, file=__FILE__, rc=rc)
  CALL ESMF_Finalize()

END PROGRAM FISOC_main
!------------------------------------------------------------------------------
